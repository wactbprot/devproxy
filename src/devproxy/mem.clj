(ns devproxy.mem
  (:require [taoensso.carmine :as car :refer (wcar)]
            [devproxy.utils :as u]
            [clojure.string :as string]
            [devproxy.conf           :as c]
            [cheshire.core :as che]
            ))

(def conn (get-in (c/config) [:redis :conn]))

;;------------------------------
;; store
;;------------------------------
(defn set-val!
  "Sets the value `v` for the key `k`."
  [k v]
  (if (and (string? k) (some? v))
    (do
      (wcar conn (car/set k (che/encode v)))
      {:ok true})
    {:error "no key or value"}))

;;------------------------------
;; del
;;------------------------------
(defn del-key!
  "Delets the key `k`."
  [k]
  (wcar conn (car/del k)))

(defn del-keys!
  "Deletes all given keys (`ks`)."
  [ks]
  (if (some? ks)
    (do 
      (run! del-key! ks)
      {:ok true})
    {:error "no keys to del"}))

;;------------------------------
;; get value(s)
;;------------------------------
(defn get-val!
  "Returns the value for the given key (`k`) and cast it to a clojure
  type."
  [k]
  (che/decode (wcar conn (car/get k)) true))

(defn pat->keys
  "Get all keys matching  the given pattern `pat`."
  [pat]
  (sort (wcar conn (car/keys pat))))


;;------------------------------
;; notification
;;------------------------------
(defn close-listener
  "Closes the given listener generated by [[gen-listener]]."
  [l]
  (car/close-listener l))

(defn gen-listener
  "Returns a listener for keyspace notifications. Close listener
  with [[close-listener]]"
  [conf pat f]
  (let [conn (get-in conf [:redis :conn :spec])]
    (car/with-new-pubsub-listener conn {pat f} (car/psubscribe pat))))
